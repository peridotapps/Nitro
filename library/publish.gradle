apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version project.versionName
group project.bintray_group_name

ext.bintray_apikey = "82e12ec8c9d4f53744ea84276a47c4acbdc4c4bc"
ext.bintray_artifact_location = "$buildDir/outputs/aar/library-release.aar"

ext.bintray_artifactid = project.bintray_artifactid
ext.bintray_description = "Create Android apps faster with a hit of Nitro"
ext.bintray_org = project.bintray_org
ext.bintray_package_name = project.bintray_package_name
ext.bintray_repo_name = project.bintray_repo
ext.bintray_user = project.bintray_user
ext.bintray_vcs_url = "https://github.com/peridotapps/" + project.bintray_vcs_url

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$bintray_artifact_location")
            groupId this.group
            artifactId "$bintray_artifactid"
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = "$bintray_user"
    key = "$bintray_apikey"
    override = true
    publications = ['Production']
    pkg {
        repo = "$bintray_repo_name"
        userOrg = "$bintray_org"
        name = "$bintray_package_name"
        description = "$bintray_description"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = "$bintray_vcs_url"
        dryRun = false
        version {
            name = this.version
            desc = "${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}